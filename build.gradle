plugins {
    id 'org.jetbrains.intellij' version "0.4.18"
}

def pluginVersion = '0.88.0'
def ideaVersion = '2019.2'
version = pluginVersion + '-' + ideaVersion

patchPluginXml {
    changeNotes """
        <ul>
            <li>You can easily download OCaml SDK sources in SDK platform settings</li>
            <li>#231 - Fix ClassCastException when reading bsconfig.json</li>
            <li>#220 - Added BuckleScript file type and icon</li> 
            <li>#219 - Added file type support for Esy</li>
            <li>Poly-variants can have a different highlighting, color scheme updated</li>
            <li>Use SVG icons</li>
            <li>#224 - Use bsb.exe instead of node.js wrapper in monorepo</li>
            <li>#221 - Compilers have separate tool windows</li> 
            <li>Use rincewind@0.8</li>
        </ul>

        <p><a href="https://github.com/reasonml-editor/reasonml-idea-plugin/blob/master/CHANGELOG.md">Full change log...</a></p>

        <p/>
        <p>To see how to integrate reason tools (bsc, refmt), go to the website.</p>
    """
}

allprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

    sourceSets {
        main {
            java.srcDirs 'src', 'gen'
            resources.srcDirs 'resources', 'plugins/resources'
        }
        test {
            java.srcDir 'tests'
        }
    }

    apply plugin: 'org.jetbrains.intellij'
    intellij {
        pluginName 'reasonml-plugin-idea'
        type 'IU'
        version '192.7142.36' // https://www.jetbrains.com/intellij-repository/releases
        updateSinceUntilBuild = false
        plugins = ['java',
                   'JavaScriptLanguage',
                   'PsiViewer:192-SNAPSHOT' // https://plugins.jetbrains.com/plugin/227-psiviewer/versions
        ]
    }

    runIde {
        systemProperty "refmtWidth", "120"
        jvmArgs = ['--add-exports=java.base/jdk.internal.vm=ALL-UNNAMED', '-Dcompiler.process.debug.port=5005', '-Xmx1024m']
    }

    task testCompilation(type: Test, group: 'Verification', dependsOn: [classes, testClasses]) {
        testLogging {
            exceptionFormat = 'full'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile project('jps-plugin')
}

apply plugin: 'idea'
idea {
    project {
        jdkName = 1.8
        languageLevel = 1.8
    }
    module {
        generatedSourceDirs += file('gen')
    }
}

verifyPlugin {
    pluginDirectory "resources"
}

/*
git log --oneline | Select-String -Pattern "(191|192|193) -" | %{$_.Line.Split(" ")[0]}
*/
