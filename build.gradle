plugins {
    id 'org.jetbrains.intellij' version "0.4.15"
}

def pluginVersion = '0.84'
def ideaVersion = '2019.1'
version = pluginVersion + '-' + ideaVersion

patchPluginXml {
    // http://www.jetbrains.org/intellij/sdk/docs/basics/getting_started/build_number_ranges.html
    // http://www.jetbrains.org/intellij/sdk/docs/reference_guide/api_changes_list.html
    sinceBuild = '191'
    untilBuild = '191.*'

    pluginDescription "Add support for ReasonML language. Also add support for OCaml, and Dune."
    changeNotes """
        <ul>
            <li>197 - java.lang.Throwable: Assertion failed: Undoable actions allowed inside commands only</li>
            <li>192 - Undoing causes Following files have changes that cannot be undone: error</li>
            <li>Fix a stub exception for functor parameter</li>
        </ul>

        <p><a href="https://github.com/reasonml-editor/reasonml-idea-plugin/blob/master/CHANGELOG.md">Full change log...</a></p>

        <p/>
        <p>To see how to integrate reason tools (bsc, refmt), go to the website.</p>
    """
}

allprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

    sourceSets {
        main {
            java.srcDirs 'src', 'gen'
            resources.srcDirs 'resources', 'plugins/resources'
        }
        test {
            java.srcDir 'tests'
        }
    }

    apply plugin: 'org.jetbrains.intellij'
    intellij {
        pluginName 'reasonml-plugin-idea'
        type 'IU'
        version '191.8026.42' // https://www.jetbrains.com/intellij-repository/releases
        updateSinceUntilBuild = false
        plugins = [
                'PsiViewer:191-SNAPSHOT' // https://plugins.jetbrains.com/plugin/227-psiviewer/versions
        ]
    }

    runIde {
        systemProperty "refmtWidth", "120"
        jvmArgs = ['--add-exports=java.base/jdk.internal.vm=ALL-UNNAMED', '-Dcompiler.process.debug.port=5005', '-Xmx1024m']
    }

    task testCompilation(type: Test, group: 'Verification', dependsOn: [classes, testClasses]) {
        testLogging {
            exceptionFormat = 'full'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile project('jps-plugin')
}

apply plugin: 'idea'
idea {
    project {
        jdkName = 1.8
        languageLevel = 1.8
    }
    module {
        generatedSourceDirs += file('gen')
    }
}

verifyPlugin {
    pluginDirectory "resources"
}
